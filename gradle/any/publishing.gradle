import edu.ucar.build.publishing.PublishingUtil

apply plugin: 'maven-publish'

// These are safe to apply to the root project. They'll basically have no effect.
PublishingUtil.addMavenPublicationsForSoftwareComponents(project)
PublishingUtil.adjustMavenPublicationPomScopes(project)

publishing {
    repositories {
        String version = rootProject.version as String
        if (version.endsWith('SNAPSHOT')) {
            maven {
                name = 'snapshots'
                url = 'https://oss.sonatype.org/content/repositories/snapshots/'
                // Set credentials in root/publishing.gradle.
                credentials {
                    username project.properties.snapshotRepoUser
                    password project.properties.snapshotRepoPass
                }
            }
        } else {
            maven {
                name = 'releases'
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                // Set credentials in root/publishing.gradle.
                credentials {
                    username project.properties.snapshotRepoUser
                    password project.properties.snapshotRepoPass
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}
